apply plugin: 'application'
apply plugin: 'java'

mainClassName = 'com.jeffheaton.opencl.HelloOpenCL'

repositories {
    mavenCentral()
}
dependencies {
    compile 'org.lwjgl.lwjgl:lwjgl:2.8.4'
}
platforms = ['windows', 'linux', 'osx']
platforms.each { platform ->
    task "${platform}Natives" {
        outputDir = "$buildDir/natives/$platform"
        inputs.files(configurations.compile)
        outputs.dir(outputDir)
        doLast {
            copy {
                def artifacts = configurations.compile.resolvedConfiguration.resolvedArtifacts
                    .findAll { it.classifier == "natives-$platform" }
                artifacts.each { 
                    from zipTree(it.file)
                }            
                into outputDir
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.4'
}

task natives {
    description "Copies native libraries to an appropriate directory."
    dependsOn platforms.collect { "${it}Natives" }.findAll { tasks[it] }
}

// Unfortunatly the Gradle app plug-in does not allow me to pass in -D options.
// LWJGL
run { 
	systemProperties = [ 'java.library.path' : './build/natives/'+getOS()]
	it.dependsOn natives
}


import org.apache.tools.ant.taskdefs.condition.Os
public String getOS() {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        return "windows"
    }
    if (Os.isFamily(Os.FAMILY_MAC)) {
        return "osx"
    }
    if (Os.isFamily(Os.FAMILY_UNIX)) {
        return "linux"
    }
    return "could not determine OS in build.gradle"
}